INCLUDE_DIRECTORIES("${PYTHON_INCLUDE_DIRS}" "${CMAKE_CURRENT_SOURCE_DIR}")

FILE(GLOB PYTHON_FILES "*.py")

FOREACH(PYTHON_FILE ${PYTHON_FILES})
  EXECUTE_PROCESS(COMMAND "${CMAKE_COMMAND}" -E copy "${PYTHON_FILE}" "${CMAKE_CURRENT_BINARY_DIR}"
                  WORKING_DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}")
ENDFOREACH(PYTHON_FILE)

SET(cdpl_MOD_SRCS
    Module.cpp
    VersionInfoExport.cpp
    ConfigInfoExport.cpp
   )

ADD_LIBRARY(_cdpl MODULE ${cdpl_MOD_SRCS})
TARGET_LINK_LIBRARIES(_cdpl ${Boost_PYTHON_LIBRARY} ${PYTHON_LIBRARIES})

SET_TARGET_PROPERTIES(_cdpl PROPERTIES PREFIX "")

IF(WIN32)
  SET_TARGET_PROPERTIES(_cdpl PROPERTIES SUFFIX ".pyd")
ELSE(WIN32)
  SET_TARGET_PROPERTIES(_cdpl PROPERTIES COMPILE_DEFINITIONS "BOOST_PYTHON_USE_GCC_SYMBOL_VISIBILITY")

  IF(NOT APPLE)
    SET_TARGET_PROPERTIES(_cdpl PROPERTIES INSTALL_RPATH "${CDPKIT_PYTHON_MODULE_INSTALL_RPATH1}")

    SET(BINARY_INPUT_FILE "${CMAKE_CURRENT_BINARY_DIR}/_cdpl.so")
    CONFIGURE_FILE("${CDPKIT_CMAKE_SCRIPTS_DIR}/InstallExternalRuntimeDependencies.cmake.in" 
                   "${CMAKE_CURRENT_BINARY_DIR}/InstallExternalRuntimeDependencies.cmake" 
                   @ONLY
                  )
    INSTALL(SCRIPT "${CMAKE_CURRENT_BINARY_DIR}/InstallExternalRuntimeDependencies.cmake")
  ELSE(NOT APPLE)
    SET_TARGET_PROPERTIES(_cdpl PROPERTIES LINK_FLAGS "-Wl,-rpath,\"${CDPKIT_PYTHON_MODULE_INSTALL_RPATH1}\"")
  ENDIF(NOT APPLE)
ENDIF(WIN32)

INSTALL(TARGETS _cdpl DESTINATION "${CDPKIT_PYTHON_INSTALL_DIR}/CDPL" COMPONENT CDPLPythonModules)
INSTALL(FILES ${PYTHON_FILES} DESTINATION "${CDPKIT_PYTHON_INSTALL_DIR}/CDPL" COMPONENT CDPLPythonModules)

ADD_SUBDIRECTORY(Base) 
ADD_SUBDIRECTORY(Math) 
ADD_SUBDIRECTORY(Util) 
ADD_SUBDIRECTORY(Chem) 
ADD_SUBDIRECTORY(Biomol) 
ADD_SUBDIRECTORY(Pharm) 
ADD_SUBDIRECTORY(Forcefield) 
ADD_SUBDIRECTORY(Grid) 
ADD_SUBDIRECTORY(Vis) 
